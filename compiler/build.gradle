plugins {
    id "java"
    id "org.jastadd" version "1.13.3"
}

// ========================================
// Debugging

tasks.withType(JavaCompile) {
    options.deprecation = true
}

// ========================================
// Testing

dependencies {
    testCompile 'junit:junit:4.12'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

sourceSets {
    test.java {
	srcDirs = [ 'test' ]
    }
}

// Configuration for the test running.
test {
  // Log passed/failed tests in the console (see also build/reports/tests):
  testLogging.events 'passed', 'failed'

  testLogging.exceptionFormat = 'full' // Full error output for test failures.

  dependsOn 'cleanTest'  // This causes tests to always be re-run.

  testLogging.showStandardStreams = true
}


// ========================================
// JastAdd

jastadd {
    configureModuleBuild()

    modules "${projectDir}/teal0/jastadd_modules", "${projectDir}/teal1/jastadd_modules",
	"${projectDir}/teal2/jastadd_modules", "${projectDir}/teal3/jastadd_modules"

    if (project.hasProperty("langVersion")) {
	module = project.getProperty("langVersion")
    } else {
	module = "teal-0"
    }

    println "Using frontend module " + module.name

    astPackage = "lang.ast"
    parser.name = "TEALParser"

    dependencies {
	implementation project(':ir') // import runtime for the compiler backend

	// JastAdd-related dependencies
	compile 'net.sf.beaver:beaver-rt:0.9.11'
	testCompile 'junit:junit:4.12'
	jflex 'de.jflex:jflex:1.6.1'
	beaver 'org.extendj:nbfront:0.1.5'
	jastadd2 'org.jastadd:jastadd:2.3.4' // Selects which JastAdd version to use (optional).
    }

    jastaddOptions = [ "--cache=all", "--tracing=compute" ]
}

jar {
    manifest {
        attributes(
            'Main-Class': 'lang.Compiler'
        )
    }
    baseName = jastadd.module.name
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    destinationDir = projectDir
}

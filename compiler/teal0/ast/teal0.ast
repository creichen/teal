// Variables
VarDecl : Decl ::= IdDecl [DeclaredType:Type] [Initializer:Expr];

// Functions
FunDecl : Decl ::= IdDecl [DeclaredReturnType:Type] Formal:VarDecl* [Body:Stmt];

// Expressions
abstract Expr;

// Binary expressions
abstract BinExpr : Expr ::= Left:Expr Right:Expr;
AddExpr : BinExpr;
SubExpr : BinExpr;
MulExpr : BinExpr;
DivExpr : BinExpr;
ModExpr : BinExpr; // %
EQExpr : BinExpr;  // ==
NEQExpr : BinExpr; // !=
LTExpr : BinExpr;  // <
GTExpr : BinExpr;  // >
LEQExpr : BinExpr; // <=
GEQExpr : BinExpr; // >=
OrExpr : BinExpr;
AndExpr : BinExpr;

// Other expressions
CallExpr : Expr ::= IdUse Actual:Expr*;
Null : Expr;
ArrayLiteralExpr : Expr ::= Expr*;
IndexExpr : Expr ::= Base:Expr Index:Expr;
NotExpr : Expr ::= Expr;
TypedExpr : Expr ::= Expr DeclaredType:Type;
NewExpr : Expr ::= Type Actual:Expr*; // Array allocation.  In later layers of Teal we generalise this operation.

// Constants
abstract Constant : Expr;
IntConstant : Constant ::= <Value:Long>;
StringConstant : Constant ::= <Value:String>;

// Variable Access
Access : Expr ::= IdUse ;

// Statements
abstract Stmt;
VarDeclStmt : Stmt ::= VarDecl;
ExprStmt : Stmt ::= Expr;
AssignStmt : Stmt ::= LValue:Expr RValue:Expr;
BlockStmt : Stmt ::= Stmt*;
IfStmt : Stmt ::= Cond:Expr Then:Stmt [Else:Stmt];
WhileStmt : Stmt ::= Cond:Expr Body:Stmt;
ReturnStmt : Stmt ::= Expr;

// Identifiers:
IdUse ::= <Identifier>;
IdDecl ::= <Identifier>;
// Unknown declaration (for error handling)
UnknownDecl : IdDecl;
